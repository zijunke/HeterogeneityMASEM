model{
  for(si in 1:Nstudy){	
    tau.ER[si]<-tau.R*N[si]/mu.N
    vR.psi[si,1:pp]~dmnorm(mu.vR.psi[1:pp],tau.vR[si,1:pp,1:pp])

    for(vi in 1:pp){
      vR[si,vi]~dnorm(vPPsi[si,vi],tau.ER[si])
      vPPsi[si,vi]<-vP[si,vi]+vR.psi[si,vi]
    }

    vP[si,1]<-a[si]
    vP[si,2]<-c[si]
    vP[si,3]<-a[si]*c[si]+b[si]*(1-a[si]*a[si])
    c[si]<-a[si]*b[si]+cp[si]
		
    a[si]<-mu.a+xi[1]*us[si,1]
    b[si]<-mu.b+xi[2]*us[si,2]
    cp[si]<-mu.cp+xi[3]*us[si,3]
    us[si,1:3]~dmnorm(u0[1:3],tau.u[1:3,1:3])
  }

  # transformed parameters
  mu.ab<-mu.a*mu.b+cov.ab
  V.ab<-pow(mu.a*sd.b,2)+pow(mu.b*sd.a,2)+pow(sd.a*sd.b,2)+pow(cov.ab,2)+2*mu.a*mu.b*cov.ab 
  sd.ab <- sqrt(V.ab)
  Vu[1:3,1:3] <-inverse(tau.u[1:3,1:3])
  sd.a<-sqrt(xi[1]*xi[1]*Vu[1,1])
  sd.b<-sqrt(xi[2]*xi[2]*Vu[2,2])
  sd.cp<-sqrt(xi[3]*xi[3]*Vu[3,3])
  rho.ab<-Vu[1,2]/sqrt(Vu[1,1]*Vu[2,2])
  rho.acp<-Vu[1,3]/sqrt(Vu[1,1]*Vu[3,3])
  rho.bcp<-Vu[2,3]/sqrt(Vu[2,2]*Vu[3,3])
  cov.ab<- xi[1]*xi[2]*Vu[1,2]

  # prior
  mu.a ~ dnorm(0,0.0001)
  mu.b ~ dnorm(0,0.0001)
  mu.cp ~ dnorm(0,0.0001)
  tau.u[1:3,1:3] ~ dwish(I3[,],4)
  xi[1] ~dgamma(2,.0001)
  xi[2] ~dgamma(2,.0001)
  xi[3] ~dgamma(2,.0001)
  tau.R ~dgamma(alpha.prior.vE,beta.prior.vE)
}


